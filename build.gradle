plugins {
    id 'java-library'
    id 'maven-publish'
}

def full_version = "${minecraft_version}-${project_version}"

group = 'be.raft.crafty'
version = full_version

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT"
}

tasks.register('sourceJar', Jar) {
    group 'build'
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
    archiveBaseName = 'CraftyBuilder'
}

build {
    dependsOn sourceJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId = 'be.raft.crafty'
            artifactId = 'crafty-builder'
            version = full_version

            from components.java
            pom {
                name = 'CraftyBuilder'
                description = 'Utility Library to make building Spigot Components more simpler'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/AtlasWorldMC/ContentWork/blob/main/LICENSE'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'AtlasWorld-Release'
            url 'https://repository.atlasworld.fr/repository/maven-release/'
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}